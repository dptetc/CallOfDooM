ACTOR IronSightSniper : Inventory { Inventory.MaxAmount 1 }
ACTOR Riflecheck : Inventory 
{
	Inventory.MaxAmount 30
	Inventory.Amount 1
}
ACTOR mp40check : Inventory 
{
	Inventory.MaxAmount 32
	Inventory.Amount 1
}
ACTOR glockcheck : Inventory 
{ 
	Inventory.MaxAmount 14
	Inventory.Amount 1
}
ACTOR cal45check : Inventory 
{ 
	Inventory.MaxAmount 8 
	Inventory.Amount 1
}
ACTOR Reload : Inventory 
{ 
	Inventory.MaxAmount 1
	Inventory.Amount 1 
}
ACTOR CoD_Weapon : DoomWeapon
{
	+CLIENTSIDEONLY
	+INVENTORY.UNDROPPABLE
	Decal "BulletChip"
	Weapon.UpSound "misc/w_pkup"
}
ACTOR NoGibPuff : BulletPuff replaces BulletPuff
{
	+NOEXTREMEDEATH
}
ACTOR KnifePuff : NoGibPuff
{
	Obituary "%k > KNIFE > %o"
	+NOBLOCKMAP
	+NOGRAVITY
	+PUFFONACTORS
	RenderStyle Translucent
	Alpha 0.6
	SeeSound "Knife/HitThing"
	AttackSound "Knife/HitWall"
	ActiveSound "Knife/Attack"
	VSpeed 1
}
ACTOR HandGrenadeAmmo : Ammo
{
	Inventory.Amount 1
	Inventory.MaxAmount 25
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 50
}
ACTOR ThrowPower : Ammo
{
	Inventory.MaxAmount 5
	Ammo.BackpackMaxAmount 5
}
ACTOR Spread : Ammo
{
	Inventory.MaxAmount 5
	Ammo.BackpackMaxAmount 5
}
ACTOR AmmoCount : Ammo
{
	Inventory.Amount 20
	Inventory.MaxAmount 300
	Ammo.BackpackAmount 20
	Ammo.BackpackMaxAmount 600
	+COUNTITEM
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.FANCYPICKUPSOUND
	States
	{
	Spawn:
		CLIP A -1
		Loop
	}
}
ACTOR FireFlash
{
	Speed 0
	Projectile
	+RANDOMIZE
	+NOCLIP
	+CLIENTSIDEONLY
	RenderStyle Add
	Alpha 1
	scale 0.25
	States
	{
	Spawn:
		FLSH ABC 2 Bright 
		Stop
	}
}
ACTOR SmokeSpawner
{
	Speed 20
	+MISSILE
	+NOBLOCKMAP
	+NOGRAVITY
	+DROPOFF
	+NOTELEPORT
	+FORCEXYBILLBOARD
	+NOTDMATCH
	+CLIENTSIDEONLY
	+GHOST
	Radius 1
	Height 1
	Mass 1
	Damage 0
	+NOCLIP
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 A 0 Thing_ChangeTID(0,390)
		TNT1 A 0 A_SpawnItem("FireFlash",0,0)
		Stop
	}
}
ACTOR Knife : CoD_Weapon
{
	Obituary "%k > KNIFE > %o"
	Weapon.KickBack 100
	+WEAPON.MELEEWEAPON
	+NOEXTREMEDEATH
	Weapon.UpSound "plats/pt1_mid"
	States
	{
	Ready:
		KNIS A 1 A_WeaponReady
		KNIS A 0 A_JumpIfInventory("Reload",1,"Reload")
		Loop
	Fire:
		KNIS B 2
		KNIS C 4 A_CustomPunch (35,0,0,"KnifePuff", 110)
		KNIS D 2
		KNIS E 2
		KNIS F 2
		KNIS G 2
		Goto Ready
	Reload:
		KNIS A 0 A_TakeInventory("Reload")
		Goto Ready
	Deselect:
		KNIS A 0 A_Lower
		KNIS A 1 A_Lower
		Loop
	Select:
		KNIS A 0 A_Raise
		KNIS A 1 A_Raise
		Loop
	}
}
ACTOR Ghost_Knife : CoD_Weapon
{
	Obituary "%k > GHOST KNIFE > %o"
	Weapon.KickBack 100
	+WEAPON.MELEEWEAPON
	+NOEXTREMEDEATH
	Alpha 0.35
	RenderStyle Translucent
	Weapon.UpSound "plats/pt1_mid"
	States
	{
	Ready:
		KNFG A 1 A_WeaponReady
		KNFG A 0 A_JumpIfInventory("Reload",1,"Reload")
		Loop
	Fire:
		KNFG B 2
		KNFG C 4 A_CustomPunch (50,0,0,"KnifePuff", 110)
		KNFG D 2
		KNFG E 2
		KNFG F 2
		KNFG E 2
		KNFG D 2
		KNFG A 2
		Goto Ready
	Reload:
		NULL A 0 A_TakeInventory("Reload")
		Goto Ready	
	Deselect:
		KNFG A 0 A_Lower
		KNFG A 1 A_Lower
		Loop
	Select:
		KNFG A 0 A_Raise
		KNFG A 1 A_Raise
		Loop
	}
}
ACTOR Sawedoff : CoD_Weapon
{
	Obituary "%k > DOUBLE BARRELED SHOTGUN > %o"
	Weapon.AmmoType1 "AmmoCount"
	Weapon.AmmoUse1 1
	AttackSound "Magnum/Fire"
	+WEAPON.NOAUTOFIRE
	Weapon.UpSound "misc/w_pkup"
	States
	{
	Ready:
		SWDF A 1 A_WeaponReady
		Goto Ready
	Deselect:
		SWDF A 0 A_Lower
		SWDF A 1 A_Lower
		Goto Deselect
	Select:
		SWDF A 0 A_Raise
		SWDF A 1 A_Raise
		Goto Select
	Fire:
		SWDF A 0 A_PlayWeaponSound("Sawedoff/Fire")
		SWDF A 0 A_FireCustomMissile("SmokeSpawner",0,0,5,0)
		SWDF A 0 A_FireBullets (12, 8, 24, 7, "NoGibPuff")
		SWDF A 0 A_SetPitch(pitch-7)
		SWDF BCDEFGHIJKLM 2
		SWDF A 0 A_PlayWeaponSound("Sawedoff/open")
		SWDF N 15 
		SWDF OPQRS 2
		SWDF A 0 A_PlayWeaponSound("Sawedoff/slide")
		SWDF TUVWXY 2
		SWDF A 0 A_PlayWeaponSound("Sawedoff/close")
		SWDR ABCBA 2
		Goto Ready
	}
}
ACTOR Glock : CoD_Weapon
{
	Obituary "%k > GLOCK > %o"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "AmmoCount"
	Weapon.AmmoUse2 1
	Weapon.AmmoType2 "AmmoCount"
	Weapon.UpSound "misc/w_pkup"
	States
	{
	Ready:
		FTRE A 1 A_WeaponReady
		FTRE A 0 A_TakeInventory("Spread",1)
		FTRE A 0 A_JumpIfInventory("Reload",1,"Reload")
		Loop
	Deselect:
		FTRE A 0 A_Lower
		FTRE A 1 A_Lower
		Loop
	Select:
		FTRE A 0 A_Raise
		FTRE A 1 A_Raise
		Loop
	Fire:
		FTRE A 0 A_JumpIfInventory("Glockcheck",1,"Firing")
		Goto Reload
	Firing:
		FTRE A 0 A_TakeInventory("Glockcheck",1)
		FTRE A 0 A_GiveInventory("Spread",1)
		FTRE A 0 A_JumpIfInventory("Spread",5,"Fire5")
		FTRE A 0 A_JumpIfInventory("Spread",4,"Fire4")
		FTRE A 0 A_JumpIfInventory("Spread",3,"Fire3")
		FTRE A 0 A_JumpIfInventory("Spread",2,"Fire2")
		FTRE A 0 A_JumpIfInventory("Spread",1,"Fire1")
		Goto Fire
	Fire5:
		FTRE A 0 A_FireBullets (8, 8, -1, 11, "NoGibPuff")
		FTRE A 0 A_SetPitch(pitch-3)
		Goto PostFire
	Fire4:
		FTRE A 0 A_FireBullets (6, 6, -1, 11, "NoGibPuff")
		FTRE A 0 A_SetPitch(pitch-3)
		Goto PostFire
	Fire3:	
		FTRE A 0 A_FireBullets (4, 4, -1, 11, "NoGibPuff")
		FTRE A 0 A_SetPitch(pitch-3)
		Goto PostFire
	Fire2:	
		FTRE A 0 A_FireBullets (2, 2, -1, 11, "NoGibPuff")
		FTRE A 0 A_SetPitch(pitch-3)
		Goto PostFire
	Fire1:	
		FTRE A 0 A_FireBullets (1, 1, -1, 11, "NoGibPuff")
		FTRE A 0 A_SetPitch(pitch-3)
		Goto PostFire
	PostFire:
		FTRE A 0 A_FireCustomMissile("SmokeSpawner",0,0,6,0)
		FTRE A 0 A_PlayWeaponSound ("Glock/Fire")
		FTRE A 2
		FTRE BCD 1 A_SetPitch(pitch+0.5)
		FTRE CBA 1 A_SetPitch(pitch+0.5)
		FTRE A 1 A_ReFire
		Goto Ready
	Reload:
		FTRE A 0 A_PlaySound("glock/out")
		FTRE EFG 3
		FTRE A 0 A_PlaySound("glock/in")
		FTRE HIJ 3
		FTRE KL 3
		FTRE A 10
		FTRE MNOP 1
		FTRE A 0 A_PlaySound("glock/cock")
		FTRE QRSTUVWX 1
		FTRE A 0 A_GiveInventory("Glockcheck",14)
		FTRE A 0 A_TakeInventory("Reload",1)
		Goto Ready
	AltFire:		
		FTRE A 0 A_JumpIfInventory("Glockcheck",1,"AltFiring")
		Goto Reload
	AltFiring:
		FTRE A 1
		FTRE A 2
		FTRE A 0 A_TakeInventory("Glockcheck",1)
		FTRE A 0 A_FireBullets (4, 4, -1, 11, "NoGibPuff")
		FTRE A 0 A_FireCustomMissile("SmokeSpawner",0,0,6,0)
		FTRE A 0 A_PlayWeaponSound ("Glock/Fire")
		FTRE A 0 A_SetPitch(pitch-3)
		FTRE A 0 A_JumpIfInventory("Glockcheck",1,2)
		Goto Reload
		FTRE B 1
		FTRE A 2
		FTRE A 0 A_TakeInventory("Glockcheck",1)
		FTRE A 0 A_FireBullets (4, 4, -1, 11, "NoGibPuff")
		FTRE A 0 A_FireCustomMissile("SmokeSpawner",0,0,6,0)
		FTRE A 0 A_PlayWeaponSound ("Glock/Fire")
		FTRE A 0 A_SetPitch(pitch-3)
		FTRE A 0 A_JumpIfInventory("Glockcheck",1,2)
		Goto Reload
		FTRE B 1
		FTRE A 2
		FTRE A 0 A_TakeInventory("Glockcheck",1)
		FTRE A 0 A_FireBullets (4, 4, -1, 11, "NoGibPuff")
		FTRE A 0 A_FireCustomMissile("SmokeSpawner",0,0,6,0)
		FTRE A 0 A_PlayWeaponSound ("Glock/Fire")
		FTRE A 0 A_SetPitch(pitch-3)
		FTRE BCD 1
		FTRE CBAAAAAAA 1 A_SetPitch(pitch+1)
		FTRE AAAAA 1
	    FTRE A 1 A_ReFire
		Goto Ready
	}
}
ACTOR Cal45 : CoD_Weapon
{
	Obituary "%k > CAL45 > %o"
	Weapon.AmmoGive1 24
	Weapon.AmmoType1 "AmmoCount"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "AmmoCount"
	Weapon.AmmoUse2 1
	+WEAPON.NOAUTOFIRE
	Weapon.UpSound "misc/w_pkup"
	States
	{
	Ready:
		CA45 D 1 A_WeaponReady
		CA45 A 0 A_TakeInventory("Spread",1)
		CA45 A 0 A_JumpIfInventory("Reload",1,"Reload")
		Loop
	Deselect:
		CA45 D 0 A_Lower
		CA45 D 1 A_Lower
		Loop
	Select:
		CA45 D 0 A_Raise
		CA45 D 1 A_Raise
		Loop
	Fire:
		CA45 A 0 A_JumpIfInventory("Cal45check",8,"Firing")
		Goto Reload
	Firing:
		CA45 A 0 A_TakeInventory("cal45check",1)
		CA45 A 0 A_GiveInventory("Spread",1)
		CA45 A 0 Bright A_PlayWeaponSound("TT/Fire")
		CA45 A 0 A_JumpIfInventory("Spread",5,"Fire5")
		CA45 A 0 A_JumpIfInventory("Spread",4,"Fire4")
		CA45 A 0 A_JumpIfInventory("Spread",3,"Fire3")
		CA45 A 0 A_JumpIfInventory("Spread",2,"Fire2")
		CA45 A 0 A_JumpIfInventory("Spread",1,"Fire1")
		Goto Fire
	Fire5:
		CA45 A 0 A_FireBullets (8, 8, -1, 12, "NoGibPuff")
		CA45 A 0 A_SetPitch(pitch-3)
		Goto PostFire
	Fire4:
		CA45 A 0 A_FireBullets (6, 6, -1, 12, "NoGibPuff")
		CA45 A 0 A_SetPitch(pitch-3)
		Goto PostFire
	Fire3:
		CA45 A 0 A_FireBullets (4, 4, -1, 12, "NoGibPuff")
		CA45 A 0 A_SetPitch(pitch-3)
		Goto PostFire
	Fire2:
		CA45 A 0 A_FireBullets (2, 2, -1, 12, "NoGibPuff")
		CA45 A 0 A_SetPitch(pitch-3)
		Goto PostFire
	Fire1:
		CA45 A 0 A_FireBullets (1, 1, -1, 12, "NoGibPuff")
		CA45 A 0 A_SetPitch(pitch-3)
		Goto PostFire
	PostFire:
		CA45 A 1 A_FireCustomMissile("SmokeSpawner",0,0,5,0)
		CA45 BCB 2 A_SetPitch(pitch+1)
		CA45 D 4
		CA45 D 2 A_ReFire
		Goto Ready
	Reload:
		CA45 A 0 A_PlaySound("TT/out")
		CA45 E 2
		CA45 F 15
		CA45 A 0 A_PlaySound("TT/in")
		CA45 GHI 4
		CA45 D 15
		CA45 JKLMN 1
		CA45 A 0 A_PlaySound("TT/cock")
		CA45 OPQRST 1
		CA45 A 0 A_GiveInventory("cal45check",99)
		CA45 A 0 A_TakeInventory("Reload",1)
		Goto Ready
	}
}
ACTOR M16 : CoD_Weapon
{
	Obituary "%k > M16 > %o"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 30
	Weapon.AmmoType1 "AmmoCount"
	Weapon.UpSound "misc/w_pkup"
	States
	{
	Ready:
		ASSU A 1 A_WeaponReady
		ASSU A 0 A_TakeInventory("Spread",1)
		ASSU A 0 A_JumpIfInventory("Reload",1,"Reload")
		Goto Ready
	Deselect:
		ASSU A 0 A_Lower
		ASSU A 1 A_Lower
		Loop
	Select:
		ASSU A 0 A_Raise
		ASSU A 1 A_Raise
		Loop
	Fire:
		ASSU A 0 A_JumpIfInventory("Riflecheck",1,"Firing")
		Goto Reload
	Firing:
		ASSU A 0 A_TakeInventory("Riflecheck",1)
		ASSU A 0 A_GiveInventory("Spread",1)
		ASSU A 0 A_JumpIfInventory("Spread",5,"Fire5")
		ASSU A 0 A_JumpIfInventory("Spread",4,"Fire4")
		ASSU A 0 A_JumpIfInventory("Spread",3,"Fire3")
		ASSU A 0 A_JumpIfInventory("Spread",2,"Fire2")
		ASSU A 0 A_JumpIfInventory("Spread",1,"Fire1")
		Goto Fire
	Fire5:
		ASSU A 0 A_FireBullets (10, 10, -1, 13, "NoGibPuff")
		ASSU A 0 A_SetPitch(pitch-4.5)
		Goto PostFire
	Fire4:
		ASSU A 0 A_FireBullets (8, 8, -1, 13, "NoGibPuff")
		ASSU A 0 A_SetPitch(pitch-4.5)
		Goto PostFire
	Fire3:
		ASSU A 0 A_FireBullets (4, 4, -1, 13, "NoGibPuff")
		ASSU A 0 A_SetPitch(pitch-4.5)
		Goto PostFire
	Fire2:
		ASSU A 0 A_FireBullets (2, 2, -1, 13, "NoGibPuff")
		ASSU A 0 A_SetPitch(pitch-4.5)
		Goto PostFire
	Fire1:
		ASSU A 0 A_FireBullets (1, 1, -1, 13, "NoGibPuff")
		ASSU A 0 A_SetPitch(pitch-4.5)
		Goto PostFire
	PostFire:
		ASSU A 0 A_PlayWeaponSound("m16/Fire")
		ASSU A 0 A_FireCustomMissile("SmokeSpawner",0,0,6,-3)
		ASSU BB 1 A_SetPitch(pitch+1)
		ASSU CC 1 A_SetPitch(pitch+1)
		ASSU B 2 A_ReFire
		Goto Ready
	Reload:
		ASSU EFG 2
		ASSU A 0 A_PlaySound("m16/out")
		ASSU HIJ 2
		ASSU J 25
		ASSU A 0 A_PlaySound("m16/in")
		ASSU IHGFEAKL 3
		ASSU A 0 A_PlaySound("m16/cock")
		ASSU MNONMLK 3
		ASSU A 0 A_GiveInventory("Riflecheck",30)
		ASSU A 0 A_TakeInventory("Reload",1)
		Goto Ready
	}
}
ACTOR AK47 : CoD_Weapon
{
	Obituary "%k > AK47 > %o"
	Weapon.KickBack 50
	Weapon.AmmoType "AmmoCount"
	Weapon.AmmoGive 30
	Weapon.AmmoUse 1
	Scale 0.55
	Weapon.UpSound "misc/w_pkup"
	STATES
	{
	Ready:
		AK47 A 1 A_WeaponReady
		AK47 A 0 A_TakeInventory("Spread",1)
		AK47 A 0 A_JumpIfInventory("Reload",1,"Reload")
		Goto Ready
	Deselect:
		AK47 A 0 A_Lower
		AK47 A 1 A_Lower
		Loop
	Select:
		AK47 A 0 A_Raise
		AK47 A 1 A_Raise
		Loop
	Fire:
		AK47 A 0 A_JumpIfInventory("Riflecheck",1,"Firing")
		Goto Reload
	Firing:
		AK47 A 0 A_TakeInventory("Riflecheck",1)
		AK47 A 0 A_GiveInventory("Spread",1)
		AK47 A 0 A_JumpIfInventory("Spread",5,"Fire5")
		AK47 A 0 A_JumpIfInventory("Spread",4,"Fire4")
		AK47 A 0 A_JumpIfInventory("Spread",3,"Fire3")
		AK47 A 0 A_JumpIfInventory("Spread",2,"Fire2")
		AK47 A 0 A_JumpIfInventory("Spread",1,"Fire1")
		Goto Fire
	Fire5:
		AK47 A 0 A_FireBullets (10, 10, -1, 13, "NoGibPuff")
		AK47 A 0 A_SetPitch(pitch-4.5)
		Goto PostFire
	Fire4:
		AK47 A 0 A_FireBullets (8, 8, -1, 13, "NoGibPuff")
		AK47 A 0 A_SetPitch(pitch-4.5)
		Goto PostFire
	Fire3:
		AK47 A 0 A_FireBullets (4, 4, -1, 13, "NoGibPuff")
		AK47 A 0 A_SetPitch(pitch-4.5)
		Goto PostFire
	Fire2:
		AK47 A 0 A_FireBullets (2, 2, -1, 13, "NoGibPuff")
		AK47 A 0 A_SetPitch(pitch-4.5)
		Goto PostFire
	Fire1:
		AK47 A 0 A_FireBullets (1, 1, -1, 13, "NoGibPuff")
		AK47 A 0 A_SetPitch(pitch-4.5)
		Goto PostFire
	PostFire:
		AK47 A 0 A_PlayWeaponSound("AK47/Fire")
		AK47 A 0 A_FireCustomMissile("SmokeSpawner",0,0,4,-3)
		AK47 BB 1 A_SetPitch(pitch+1)
		AK47 CC 1 A_SetPitch(pitch+1)
		AK47 B 2 A_ReFire
		Goto Ready
	Reload:
		AK47 DEFG 3
		AK47 A 0 A_PlaySound("ak47/out")
		AK47 G 25
		AK47 A 0 A_PlaySound("ak47/in")
		AK47 GHIJKLMN 3
		AK47 A 0 A_PlaySound("ak47/cock")
		AK47 OPQRST 2
		AK47 UVW 3
		AK47 A 0 A_GiveInventory("Riflecheck",30)
		AK47 A 0 A_TakeInventory("Reload",1)
		Goto Ready
	}
}
ACTOR MP40 : CoD_Weapon
{
	Obituary "%k > MP40 > %o"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 30
	Weapon.AmmoType "AmmoCount"
	AttackSound "DSTOMFR"
	Weapon.UpSound "misc/w_pkup"
	States
	{
	Select:
		MP40 A 0 A_Raise
		MP40 A 1 A_Raise
		Loop
	Deselect:
		MP40 A 0 A_Lower
		MP40 A 1 A_Lower
		Loop
	Ready:
		MP40 A 1 A_WeaponReady
		MP40 A 0 A_TakeInventory("Spread",1)
		MP40 A 0 A_JumpIfInventory("Reload",1,"Reload")
		Loop
	Fire:
		MP40 A 0 A_JumpIfInventory("Mp40check",1,"Firing")
		Goto Reload
	Firing:
		MP40 A 0 A_TakeInventory("mp40check",1)
		MP40 A 0 A_GiveInventory("Spread",1)
		MP40 A 0 A_JumpIfInventory("Spread",5,"Fire5")
		MP40 A 0 A_JumpIfInventory("Spread",4,"Fire4")
		MP40 A 0 A_JumpIfInventory("Spread",3,"Fire3")
		MP40 A 0 A_JumpIfInventory("Spread",2,"Fire2")
		MP40 A 0 A_JumpIfInventory("Spread",1,"Fire1")
		Goto Fire
	Fire5:
		MP40 A 0 A_FireBullets (9, 9, -1, 13, "NoGibPuff")
		MP40 A 0 A_SetPitch(pitch-4)
		Goto PostFire
	Fire4:	
		MP40 A 0 A_FireBullets (8, 8, -1, 13, "NoGibPuff")
		MP40 A 0 A_SetPitch(pitch-4)
		Goto PostFire
	Fire3:
		MP40 A 0 A_FireBullets (6, 6, -1, 13, "NoGibPuff")
		MP40 A 0 A_SetPitch(pitch-4)
		Goto PostFire
	Fire2:	
		MP40 A 0 A_FireBullets (3, 3, -1, 13, "NoGibPuff")
		MP40 A 0 A_SetPitch(pitch-4)
		Goto PostFire
	Fire1:
		MP40 A 0 A_FireBullets (1, 1, -1, 13, "NoGibPuff")
		MP40 A 0 A_SetPitch(pitch-4)
		Goto PostFire
	PostFire:
		MP40 A 0 A_PlayWeaponSound("MP40/Fire")
		MP40 A 0 A_FireCustomMissile("SmokeSpawner",0,0,7,0)
		MP40 B 0
		MP40 CC 1 A_SetPitch(pitch+1)
		MP40 BB 1 A_SetPitch(pitch+1)
		MP40 D 2 A_ReFire
		Goto Ready
	Reload:
		MP40 EF 3
		MP40 A 0 A_PlaySound("MP40/out")
		MP40 G 20
		MP40 HI 3
		MP40 A 0 A_PlaySound("MP40/in")
		MP40 JKLM 3
		MP40 A 0 A_PlaySound("MP40/cock")
		MP40 NOPQR 3
		MP40 A 0 A_GiveInventory("mp40check",32)
		MP40 A 0 A_TakeInventory("Reload",1)
		Goto Ready
	}
}
ACTOR Minigun : CoD_Weapon
{
	Obituary "%k > MINIGUN > %o"
	Weapon.AmmoUse1 1
	Weapon.AmmoGive1 30
	Weapon.AmmoType1 "AmmoCount"
	AttackSound "DSTOMFR"
	Weapon.UpSound "misc/w_pkup"
	States
	{
	Select:
		SPRT A 0 A_Raise
		SPRT A 1 A_Raise
		Loop
	Deselect:
		SPRT A 0 A_Lower
		SPRT A 1 A_Lower
		Loop
	Ready:
		SPRT A 1 A_WeaponReady
		Loop
	Fire:
		SPRT A 0 A_FireCustomMissile("SmokeSpawner",0,0,0,0)
		SPRT A 1 Bright
		SPRT A 0 A_FireBullets (8, 8, -1, 13, "NoGibPuff")
		SPRT A 0 A_PlayWeaponSound("Minigun/Fire")
		SPRT A 0 A_SetPitch(pitch-4)
		SPRT BC 1 A_SetPitch(pitch+1.9)
		SPRT A 0 A_FireCustomMissile("SmokeSpawner",0,0,0,0)
		SPRT D 1 Bright
		SPRT A 0 A_FireBullets (8, 8, -1, 13, "NoGibPuff")
		SPRT A 0 A_PlayWeaponSound("Minigun/Fire")
		SPRT A 0 A_SetPitch(pitch-4)
		SPRT EF 1 A_SetPitch(pitch+1.9)
		SPRT A 1 A_ReFire
		Goto Ready
	}
}
ACTOR HandGrenade : CoD_Weapon
{
	Obituary "%k > GRENADE > %o"
	Inventory.PickupMessage "You got a hand grenade!"
	Weapon.AmmoType "HandGrenadeAmmo"
	Weapon.AmmoGive 1
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "ThrowPower" 
	Weapon.AmmoGive2 0 
	Weapon.AmmoUse2 0 
	Weapon.UpSound "plats/pt1_mid"
	States
	{
	Ready: 
		HNDG A 1 A_WeaponReady
		Loop
	Deselect:
		HNDG A 0 A_JumpIfInventory("HandGrenadeAmmo",1,"Deselect2")
		HNDG A 1 A_Lower
		Loop
	Deselect2:
		HNDG A 0 A_Lower
		HNDG A 1 A_Lower
		Loop
	Select:
		HNDG A 0 A_TakeInventory("ThrowPower",999) 
		HNDG A 0 A_Raise
		HNDG A 1 A_Raise
		Loop
	Fire:
		HNDG A 0 A_JumpIfInventory("ThrowPower",1,9)
		HNDG BCD 2
		HNDG E 2 A_PlaySound("Grenade/PullPin")
		HNDG FGHH 2
		NULL A 10 A_GiveInventory("ThrowPower",1)
		HNDG A 0 A_ReFire
		HNDG I 2 A_PlaySound("Grenade/Toss") 
		HNDG A 0 A_JumpIfInventory("ThrowPower",5,5)
		HNDG A 0 A_JumpIfInventory("ThrowPower",4,5)
		HNDG A 0 A_JumpIfInventory("ThrowPower",3,5)
		HNDG A 0 A_JumpIfInventory("ThrowPower",2,5)
		HNDG A 0 A_JumpIfInventory("ThrowPower",1,5) 
		HNDG J 0 A_FireCustomMissile("ThrownGrenade5",0,1,0,0)
		Goto PostFire
		HNDG J 0 A_FireCustomMissile("ThrownGrenade4",0,1,0,0)
		Goto PostFire
		HNDG J 0 A_FireCustomMissile("ThrownGrenade3",0,1,0,0)
		Goto PostFire
		HNDG J 0 A_FireCustomMissile("ThrownGrenade2",0,1,0,0)
		Goto PostFire
		HNDG J 0 A_FireCustomMissile("ThrownGrenade1",0,1,0,0)
		Goto PostFire
	PostFire:
		HNDG J 0 A_PlaySound("Grenade/Toss")
		HNDG JKLMNOPQ 2
		HNDG A 0 A_TakeInventory("ThrowPower",999)
		NULL A 12 A_CheckReload
		HNDG XWVUTS 1
		Goto Ready
	}
}
ACTOR ThrownGrenade1
{
	Obituary "%k > GRENADE > %o"
	Radius 5
	Height 5
	Speed 15
	Damage 10
	Scale 0.3
	Gravity 0.7
	SeeSound "Grenade/Bounce"
	PROJECTILE
	+HEXENBOUNCE
	-NOGRAVITY
	States
	{
	Spawn:
		HGRN A 4
		HGRN BCDEFABCDEFABCDEF 4
		Goto Death
	Death:
		HGRN A 0 A_PlaySound("Grenade/Explode")
		HGRN A 0 A_Explode (150, 250, 1)
		HGRN A 0 A_SpawnItem("GrenadeExplosion")
		Stop
	}
}
ACTOR GrenadeExplosion
{
	RenderStyle Add
	Alpha 0.5
	States
	{
	Spawn:
		EXPL ABCDEFGHIJKLMNOPQ 2 BRIGHT
		Stop
	}
}
ACTOR ThrownGrenade2 : ThrownGrenade1
{
	Speed 30
}
ACTOR ThrownGrenade3 : ThrownGrenade1
{
	Speed 43
}
ACTOR ThrownGrenade4 : ThrownGrenade1
{
	Speed 52
}
ACTOR ThrownGrenade5 : ThrownGrenade1
{
	Speed 60
}
ACTOR ThrownGrenade6 : ThrownGrenade1
{
	Speed 71
}
ACTOR M40 : CoD_Weapon
{
	Obituary "%k > M40 > %o"
	Radius 20
	Height 16
	Weapon.KickBack 125
	Weapon.AmmoType1 "AmmoCount"
	Weapon.AmmoUse1 1
	Weapon.AmmoGive1 12
	Weapon.AmmoType2 ""
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
	Scale 0.80
	+WEAPON.NOAUTOFIRE
	Weapon.UpSound "misc/w_pkup"
	states
	{
	Ready:
		SNPG A 1 A_WeaponReady
		Loop
	Ready2:
		SNPS A 1 A_WeaponReady(1)
		Loop
	Deselect:
		SNPG A 0 A_Lower
		SNPG A 1 A_Lower
		Loop
	Select:
		SNPG A 0 A_Raise
		SNPG A 1 A_Raise
		Loop
	Fire:
		SNPG A 0 A_JumpIfInventory("IronSightSniper",1,"Fire2")
		SNPG A 0 A_FireBullets (1, 1, -1, 70, "NoGibPuff")
		SNPG A 0 A_SetPitch(pitch-20)
		SNPG A 0 A_PlayWeaponSound("M40Rifle/Fire")
		SNPG A 0 A_FireCustomMissile("SmokeSpawner",0,0,2,0)
		SNPF AAABBBCCC 1 A_SetPitch(pitch+1)
		SNPG A 1 A_SetPitch(pitch+1)
		SNPG AA 1 A_SetPitch(pitch+1)
		SNPG BBBCCC 1 A_SetPitch(pitch+1)
		SNPG DD 1 A_SetPitch(pitch+1)
		SNPG EF 2 
		SNPG H 2 A_PlaySoundEx("M40Rifle/Bolt1",1)
		SNPG IJ 2
		SNPG K 3
		SNPG JI 2
		SNPG H 2 A_PlaySoundEx("M40Rifle/Bolt2",2)
		SNPG FED 3
		SNPG CB 4
		SNPG A 2
		SNPG A 1 A_ReFire
		Goto Ready
	Fire2:
	    SNPG A 0 A_FireBullets (1, 1, -1, 70, "NoGibPuff")
		SNPS A 0 A_PlayWeaponSound("M40Rifle/Fire")
		SNPS A 0 A_SetPitch(pitch-20)
		SNPS AAAAAAAAA 1 A_SetPitch(pitch+1)
		SNPS A 1 A_SetPitch(pitch+1)
		SNPS AA 1 A_SetPitch(pitch+1)
		SNPS AAACCC 1 A_SetPitch(pitch+1)
		SNPS AA 1 A_SetPitch(pitch+1)
		SNPS AA 2 
		SNPS AAA 2
		SNPS A 2 A_PlaySoundEx("M40Rifle/Bolt1",1)
		SNPS AA 2
		SNPS A 3
		SNPS AA 2
		SNPS A 2 A_PlaySoundEx("M40Rifle/Bolt2",2)
		SNPS AAA 3
		SNPS AA 4
		SNPS A 2
		SNPS A 1 A_ReFire
		Goto Ready2
	AltFire:
		SNPS A 0
		SNPS A 0 A_JumpIfInventory("IronSightSniper",1,"AltFire2")
		SNPS A 0 A_Giveinventory("IronSightSniper",1)
		SNPS A 0 A_PlaySound ("SRZOOM", CHAN_WEAPON)
		SNPS A 0 A_ZoomFactor(4.0, ZOOM_INSTANT)
		SNPS A 1
		Goto Ready2
	AltFire2:
		SNPS A 1
		SNPS A 0 A_Takeinventory("IronSightSniper",1)
		SNPS A 0 A_PlaySound ("SRZOOM", CHAN_WEAPON)
		SNPS A 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
		Goto Ready
	}
}