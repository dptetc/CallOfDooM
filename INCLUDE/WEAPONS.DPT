
ACTOR ReloadActive : CustomInventory
{
	Inventory.Amount 1
	Inventory.MaxAmount 1
	-INVBAR
	States
	{
	Use:
		NULL A 0 A_GiveInventory("IsReloading",1)
		Fail
	}
}

ACTOR ReloadCancel : CustomInventory
{
	Inventory.Amount 1
	Inventory.MaxAmount 1
	-INVBAR
	States
	{
	Use:
		NULL A 0 A_TakeInventory("IsReloading",1)
		Fail
	}
}

ACTOR IsReloading : Inventory
{
	Inventory.MaxAmount 1
}

ACTOR UnloadActive : CustomInventory
{
	Inventory.Amount 1
	Inventory.MaxAmount 1
	-INVBAR
	States
	{
	Use:
		NULL A 0 A_GiveInventory("IsUnloading",1)
		Fail
	}
}

ACTOR UnloadCancel : CustomInventory
{
	Inventory.Amount 1
	Inventory.MaxAmount 1
	-INVBAR
	States
	{
	Use:
		NULL A 0 A_TakeInventory("IsUnloading",1)
		Fail
	}
}

ACTOR IsUnloading : Inventory
{
	Inventory.MaxAmount 1
}

ACTOR Energy : Ammo
{
	Inventory.Amount 20
	Inventory.MaxAmount 300
	Ammo.BackpackAmount 20
	Ammo.BackpackMaxAmount 600
	Inventory.Icon "ENRGA0"
	+FLOATBOB
	Inventory.PickupSound "ENERGY"
	States
	{
	Spawn:
		ENRG A -1
		Stop
	}
}

ACTOR Fireflash
{
	Speed 0
	Projectile
	+RANDOMIZE
	RenderStyle Add
	Alpha 0.75
	scale 0.25
	States
	{
	Spawn:
		FLSH ABC 2 Bright
		Stop
	}
}

ACTOR SmokeSpawner
{
	Speed 20
	+MISSILE
	+NOBLOCKMAP
	+NOGRAVITY
	+DROPOFF
	+NOTELEPORT
	+FORCEXYBILLBOARD
	+NOTDMATCH
	+GHOST
	radius 1
	height 1
	mass 1
	damage 0
	+NOCLIP
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 A 0 Thing_ChangeTID(0,390)
		TNT1 A 0 A_SpawnItem("FireFlash",0,0)
		Stop
	}
}

ACTOR CheapAlert
{
	Health 1000
	Radius 1
	Height 1
	Speed 0
	Damage 0
	Mass 1
	PROJECTILE
	States
	{
	Spawn:
	Death:
		NULL AA 1 A_AlertMonsters
		Stop
	}
}

ACTOR Wrench : Weapon
{
	Radius 20
	Height 12
	Scale 0.5
	+WEAPON.NOAUTOFIRE
	+INVENTORY.UNDROPPABLE
	+NOALERT
	Weapon.AmmoType "SentryAmmo"
	Weapon.AmmoUse 1
	States
	{
	Select:
		PIPW A 0 A_Raise
		PIPW A 1 A_Raise
		Loop
	Deselect:
		PIPW A 0 A_Lower
		PIPW A 1 A_Lower
		Loop
	Ready:
		PIPW A 25 A_WeaponReady
		PIPW A 1 A_GunFlash
		Loop
	NoAmmo:
		PIPW A 1
		Goto Ready
	Fire:
		PIPW A 0 A_JumpIfNoAmmo("NoAmmo")
		PIPW A 0 A_JumpIf(ACS_ExecuteWithResult(800), "NoAmmo") // Limit max turrets.
		PIPW DEF 1 A_AlertMonsters
		PIPW G 3 A_SpawnItemEx ("SentryGun", 56, 0, 8, 0, 0, 0, 0, 1)
		PIPW G 0 A_TakeInventory("SentryAmmo",1)
		PIPW HI 1
		PIPW C 1
		PIPW BA 1 // ReFire must be decreased just because the flag NoautoFire doesn't work and player goes mad with burst.
		Goto Ready
	Flash:
		TNT1 A 50 A_SpawnItemEx("SentryGunShadow", 56, 0, 8, 0, 0, 0, 0, 1)
		Loop
	}
}

Actor SentryGunShadow
{
	RenderStyle Translucent
	Alpha 0.3
	+NOCLIP
	+FLOORHUGGER
	States
	{
	Spawn:
		SNTS A 10 A_FadeOut(0.1)
		Stop
	}
}

actor SentryGun
{
	obituary "%o was ventilated by an auto-sentry."
	Health 50
	radius 20
	height 56
	mass 100000
	speed 0
	MaxTargetRange 65536
	DamageFactor "ElectricBomb", 999
	attacksound "sentry/attack"
	DamageFactor "HellishStrike", 0
	Monster
	+NOICEDEATH
	+NOINFIGHTING
	+TELESTOMP
	-NOTDMATCH
	-COUNTKILL
	+ISMONSTER
	+FORCEYBILLBOARD
	+DONTGIB
	+MISSILEMORE
	+MISSILEEVENMORE
	-SOLID
	+AMBUSH
	+NOICEDEATH
	+FLOORCLIP
	+FRIENDLY
	+NOBLOOD
	states
	{
	Spawn:
		NULL A 0
		NULL A 0 ACS_ExecuteAlways(801, 0) // Sentry spawn script.
		Goto Stay
	Stay:
		SENT AAAAAAAAAAAAAAAA 4 A_Look
		SENT A 0 A_PlaySound ("Sentry/Active")
		loop
	See:
		SENT AAAAAAAAAAAAAAAA 4 A_Chase
		SENT A 0 A_PlaySound ("Sentry/Active")
		loop
	Missile:
		SENT A 4 A_FaceTarget
		SENT A 0 A_PosAttack
		SENT B 4 bright A_PosAttack
		SENT A 4 A_CposRefire
		goto Missile+1
	Death:
		NULL A 0 ACS_ExecuteAlways(802, 0) // Sentry death script.
		SENT D 1 A_PlaySoundEx ("Sentry/Death", "SoundSlot5")
		SENT D 0 A_PlaySoundEx ("weapons/rocklx", "SoundSlot6")
		SENT D 0 A_SpawnItem ("SentryBoom", 0, 48)
		SENT D 500
	FadeOut:
		SENT D 2 A_FadeOut(0.1)
		loop
	}
}

Actor SentryAmmo : Ammo
{
	Inventory.Amount 3
	Inventory.MaxAmount 3
	Ammo.BackpackAmount 3
	Ammo.BackpackMaxAmount 3
	States
	{
	Spawn:
		MSBO A -1
		Stop
	}
}

actor SentryBoom
{
	radius 2
	height 2
	+NOINTERACTION
	states
	{
	Spawn:
		MISL B 1 A_PlaySound("DispenserBlowup")
		MISL CDBCDBCDBCDBCDBCD 1 A_SpawnItemEx("CyborgExplosion", 0, 0, 32, random(-10,10), random(-10,10), random(-10,10))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("RandomCyborgChunk", 0, 0, 36, random(-35,35), random(-35,35), random(-10,30))
		TNT1 AAAAAAAAAAAAA 0 A_SpawnItemEx("CyborgExplosion", 0, 0, 32, random(-5,5), random(-5,5), random(-5,5))
		TNT1 A 1
		Stop
	}
}

ACTOR MiniBotBox : CustomInventory
{
	+COUNTITEM
	+INVENTORY.INVBAR
	Inventory.MaxAmount 3
	States
	{
	Use:
		TNT1 A 1 A_SpawnItemEx ("MiniBot", 56, 0, 8, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR MiniBot
{
	Obituary "%k > MINIBOT > %o"
	Health 50
	Radius 20
	Height 56
	Speed 7
	scale 0.7
	PainChance 200
	Monster
	-ISMONSTER
	+FLOORCLIP
	+FRIENDLY
	+NEVERRESPAWN
	+NOPAIN
	AttackSound "grunt/attack"
	DropItem "Energy"
	States
	{
	Spawn:
		PGRD AB 4 A_Look
		Loop
	See:
		PGRD AAAABBBBCCCCDDDD 2 A_Chase
		Loop
	Missile:
		PGRD A 0 A_FaceTarget
		PGRD FF 0 A_CPosAttack
		PGRD F 2 BRIGHT A_CPosRefire
		Goto Missile+1
	XDeath:
		PGRD A 0 A_NoBlocking
		PGRD A 0 A_PlaySound("weapons/grenadeexplode")
		PGRD A 0 A_SkullPop("Custom_BloodySkull3")
		PGRD A 0 A_SpawnItemEx("CYBGIBBER", 0, 0, 0, 0, 0, 0, 0, 161, 0)
		PGRD A 0 A_SpawnItemEx("CyborgExplosion", 0, 0, 0, 0, 0, 0, 0, 160, 0)
		PGRD A 1
		NULL A -1
	Death:
		PGRD H 2
		PGRD I 2 A_Scream
		PGRD J 2 A_NoBlocking
		PGRD KLMNOPQRST 2
		PGRD T -1
		Stop
	}
}

ACTOR CratePack : Weapon
{
	weapon.ammotype "CrateAmmo"
	weapon.ammotype2 "CrateAmmo"
	weapon.ammouse 1
	Weapon.ammouse2 1
	attacksound "knife/attack"
	+WEAPON.NOAUTOFIRE
	+INVENTORY.UNDROPPABLE
	states
	{
	Ready:
		AMMA A 1 A_WeaponReady
		Loop
	Deselect:
		AMMA A 0 A_Lower
		AMMA A 1 A_Lower
		Loop
	Select:
		AMMA A 0 A_Raise
		AMMA A 1 A_Raise
		Loop
	Fire:
		TNT1 A 3
		TNT1 A 0 A_FireCustomMissile("CrateSpawn",0)
		TNT1 A 3
		Goto Ready
	}
}

ACTOR CrateAmmo : Ammo
{
	Inventory.Amount 1
	Inventory.MaxAmount 2
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 2
	States
	{
	Spawn:
		AMMO A -1
		Stop
	}
}

ACTOR CrateSpawn
{
	Obituary "%k > CRATE > %o"
	Radius 8
	Height 8
	Speed 6
	Scale 0.5
	Projectile
	-NOGRAVITY
	+PUFFONACTORS
	+RANDOMIZE
	SeeSound "knife/attack"
	Damage 10
	States
	{
	Spawn:
		BPAK A 1
		Loop
	Death:
		TNT1 A 0 A_SpawnItem("BackPack")
		Stop
	}
}

ACTOR MedikitPack : Weapon
{
	weapon.ammotype "MedkitAmmo"
	weapon.ammouse 1
	scale 0.15
	attacksound "knife/attack"
	+WEAPON.NOAUTOFIRE
	+INVENTORY.UNDROPPABLE
	states
	{
	Ready:
		AIDA A 1 A_WeaponReady
		Loop
	Deselect:
		AIDA A 0 A_Lower
		AIDA A 1 A_Lower
		Loop
	Select:
		AIDA A 0 A_Raise
		AIDA A 1 A_Raise
		Loop
	Fire:
		TNT1 A 1 A_JumpIfNoAmmo("Ready")
		TNT1 A 2
		TNT1 C 0 A_FireCustomMissile("MedkitSpawn",0)
		TNT1 A 3
		Goto Ready
	}
}

ACTOR MedkitAmmo : Ammo
{
	Inventory.Amount 1
	Inventory.MaxAmount 3
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 6
	States
	{
	Spawn:
		MEDI A -1
		Stop
	}
}

ACTOR SpecialMedkit : Medikit
{
	Inventory.Amount 50
}

ACTOR MedkitSpawn
{
	Obituary "%k > MEDIKIT > %o"
	Radius 8
	Height 8
	Speed 15
	Damage 10
	Projectile
	-NOGRAVITY
	+PUFFONACTORS
	+RANDOMIZE
	SeeSound "knife/attack"
	States
	{
	Spawn:
		MEDI A 1
		Loop
	Death:
		TNT1 A 0 A_SpawnItem("SpecialMedkit",0)
		Stop
	}
}

ACTOR Knife : Weapon
{
	Obituary "%k > KNIFE > %o"
	Weapon.Kickback 100
	+WEAPON.WIMPY_WEAPON
	+NOALERT
	+WEAPON.MELEEWEAPON
	+NOEXTREMEDEATH
	+INVENTORY.UNDROPPABLE
	States
	{
	Ready:
		KNIS A 1 A_WeaponReady
		Loop
	Fire:
		KNIS B 2
		KNIS C 4 A_CustomPunch (35,0,0,"KnifePuff", 110)
		KNIS D 2
		KNIS E 2
		KNIS F 2
		KNIS G 2
		Goto Ready
	Deselect:
		KNIS A 0 A_Lower
		KNIS A 1 A_Lower
		Loop
	Select:
		KNIS A 0 A_Raise
		KNIS A 1 A_Raise
		Loop
	}
}

ACTOR Stealth_Knife : Weapon
{
	Obituary "%k > KNIFE > %o"
	Weapon.Kickback 100
	+WEAPON.WIMPY_WEAPON
	+NOALERT
	+WEAPON.MELEEWEAPON
	+NOEXTREMEDEATH
	Alpha 0.35
	RenderStyle Translucent
	+INVENTORY.UNDROPPABLE
	States
	{
	Ready:
		KNFG A 1 A_WeaponReady
		Loop
	Fire:
		KNFG B 2
		KNFG C 4 A_CustomPunch (50,0,0,"KnifePuff", 110)
		KNFG D 2
		KNFG E 2
		KNFG F 2
		KNFG E 2
		KNFG D 2
		KNFG A 2
		Goto Ready
	Deselect:
		KNFG A 0 A_Lower
		KNFG A 1 A_Lower
		Loop
	Select:
		KNFG A 0 A_Raise
		KNFG A 1 A_Raise
		Loop
	}
}

ACTOR KnifePuff
{
	+NOBLOCKMAP
	+NOGRAVITY
	+NOEXTREMEDEATH
	+PUFFONACTORS
	RenderStyle Translucent
	Alpha 0.6
	SeeSound "Knife/HitThing"
	AttackSound "Knife/HitWall"
	ActiveSound "Knife/Miss"
	VSpeed 1
	States
	{
	Spawn:
		PUFF ABCD 4
		Stop
	}
}

Actor ChainsawWorking : Inventory
{
	Inventory.MaxAmount 1
}

ACTOR ChainsawSmoke
{
	+NOCLIP
	+MISSILE
	+NOGRAVITY
	RENDERSTYLE ADD
	ALPHA 0.7
	Scale 0.5
	Speed 2
	States
	{
	Spawn:
		SMK2 FABCDE 4
		Stop
	}
}

ACTOR ChainsawCheck : Inventory
{
	Inventory.Amount 1
	Inventory.MaxAmount 2
}

ACTOR BetaChainsaw : Weapon
{
	Weapon.AmmoUse 0
	Weapon.Upsound 0
	Weapon.Readysound 0
	Weapon.AmmoType1 "Energy"
	Weapon.AmmoGive1 50
	Decal ChainsawMark
	+MELEEWEAPON
	+NOALERT
	+INVENTORY.UNDROPPABLE
	States
	{
	Spawn:
		CSAW A -1
		stop
	Deselect:
		SAWG A 0 A_TakeInventory("ChainsawWorking",1)
		SAWG C 0 A_Lower
		SAWG C 1 A_Lower
		Loop
	Select:
		SAWG C 0 A_Raise
		SAWG C 1 A_Raise
		Loop
	Ready:
		SAWG C 1 A_WeaponReady
		Loop
	Ready2:
		SAWG C 0 A_PlayWeaponSound ("weapons/sawidle")
		SAWG C 4 A_WeaponReady
		SAWG C 0 A_AlertMonsters
		SAWG A 0 A_TakeInventory("Energy",1)
		SAWG D 0 A_FireCustomMissile("ChainsawSmoke",-60,0,0,0)
		SAWG D 4 A_WeaponReady
		Loop
	Fire:
		SAWG A 0 A_JumpIfInventory("Energy",1, "Fire2")
		SAWG F 0 A_PlayWeaponSound("weapons/sawup")
		SAWG A 0 A_GiveInventory("ChainsawWorking",1)
		SAWG A 0 A_PlayWeaponSound ("weapons/sawidle")
		SAWG FGHGF 3
		SAWG A 0 A_AlertMonsters
		Goto Ready
	Fire2:
		SAWG A 0 A_JumpIfInventory("ChainsawWorking",1,"TrueFire")
		SAWG F 0 A_PlayWeaponSound("weapons/sawup")
		SAWG A 0 A_GiveInventory("ChainsawWorking",1)
		SAWG A 0 A_PlayWeaponSound ("weapons/sawidle")
		SAWG A 0 A_FireCustomMissile("ChainsawSmoke",-60,0,0,0)
		SAWG FGHGF 3
		SAWG A 0 A_AlertMonsters
		Goto Ready2
	TrueFire:
		SAWG A 0 A_TakeInventory("Energy",1)
		SAWG A 0 A_AlertMonsters
		SAWG A 0 A_FireCustomMissile("ChainsawSmoke",-60+random(-4,4),0,0,0)
		SAWG A 0 Radius_Quake(2,3,0,2,1)
		SAWG A 3 A_Saw
		SAWG B 0 A_FireCustomMissile("ChainsawSmoke",-60+random(-4,4),0,0,0)
		SAWG B 3 A_Saw
		SAWG A 0 A_Refire
		Goto Ready2
	Altfire:
		SAWG A 0 A_TakeInventory("ChainsawWorking",1)
		Goto Ready
	}
}

ACTOR MagnumAmmo : Ammo
{
	Inventory.MaxAmount 6
}

ACTOR Magnum : Weapon
{
	+AMMO_OPTIONAL
	+NOAUTOFIRE
	Weapon.AmmoType1 "Energy"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "MagnumAmmo"
	Weapon.AmmoUse2 0
	AttackSound "magnum/fire"
	Decal BulletChip
	States
	{
	Ready:
		REVG A 0 A_JumpIfInventory("IsReloading",1,"Reload")
		REVG A 1 A_WeaponReady
		Goto Ready
	Deselect:
		REVG A 0 A_Lower
		REVG A 1 A_Lower
		Goto Deselect
	Select:
		REVG A 0 A_Raise
		REVG A 1 A_Raise
		Goto Select
	Fire:
		REVG A 0 A_JumpIfNoAmmo("DryFire")
		REVG A 2 BRIGHT A_FireBullets(2,2,-1,35,"BulletPuff",1)
		REVG B 1 A_FireCustomMissile("SmokeSpawner",0,0,4,6)
		REVG CD 1
		REVG E 2
		REVG B 2
		REVG A 4
		Goto Ready
	DryFire:
		REVG A 8 A_PlaySound("empty/revolver")
		REVG A 1
		Goto Reload
	Reload:
		REVG A 0 A_JumpIfInventory("MagnumAmmo", 0, "Ready")
		REVG A 0 A_JumpIfInventory("Energy", 1, 1)
		Goto Ready
		REVR ABC 1
		REVR D 2 A_PlaySound("magnum/open")
		REVR EFGGHHH 2
		REVR G 5 A_PlaySound("magnum/out") 
		REVR I 2
		REVR J 8
		REVR KIJ 2
		REVR G 2 A_PlaySound("magnum/in")
		REVR FED 2
		REVR CBA 3
	ReloadLoop:
		REVR K 0 A_TakeInventory("MagnumAmmo", 1)
		REVR K 0 A_GiveInventory("Energy", 1)
		REVR I 0 A_JumpIfInventory("Energy",6,"ReloadFinish")
		REVR I 0 A_JumpIfInventory("MagnumAmmo",1,"ReloadLoop")
	ReloadFinish:
		REVG A 1
		Goto Ready
	}
}

ACTOR Colt.45 : Weapon
{
	Obituary "%k > COLT.45 > %o"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "Energy"
	+WEAPON.WIMPY_WEAPON
	+INVENTORY.UNDROPPABLE
	States
	{
	Ready:
		COLT A 1 A_WeaponReady
		Loop
	Deselect:
		COLT A 0 A_Lower
		COLT A 1 A_Lower
		Loop
	Select:
		COLT A 0 A_Raise
		COLT A 1 A_Raise
		Loop
	Fire:
		COLT A 0 A_PlaySound ("COLT_M1911/Fire", CHAN_WEAPON)
		COLF A 1 A_FireBullets(5.2,1,1,7.5)
		COLF A 1 A_Light2
		COLF A 1 A_Light1
		COLT B 3 A_Light0
		COLT CDE 3
		COLT B 3 A_ReFire
		Goto Ready
	Flash:
		COLF A 3 Bright A_Light1
		COLF A 3 Bright A_Light0
		Goto LightDone
	}
}

ACTOR LugerClipIn : Inventory { Inventory.MaxAmount 8 }

ACTOR Luger : Weapon
{
	Obituary "%k > LUGER > %o"
	Weapon.AmmoType "Energy"
	Weapon.AmmoGive 8
	Weapon.AmmoUse 0
	AttackSound "Luger/Fire"
	Scale 0.45
	+INVENTORY.UNDROPPABLE
	States
	{
	Ready:
		LUGG A 1 A_WeaponReady
		NULL A 0 A_JumpIfInventory("IsReloading", 1, "Reload")
		Loop
	Deselect:
		LUGG A 0 A_Lower
		LUGG A 1 A_Lower
		Loop
	Select:
		LUGG A 0 A_Raise
		LUGG A 1 A_Raise
		Loop
	Fire:
		LUGG A 1 A_JumpIfInventory("LugerClipIn", 1, 1)
		Goto Reload
		LUGG A 1
		LUGE A 2 Bright A_FireBullets (5,5,1,16,"BulletPuff")
		LUGE B 0 Bright A_FireCustomMissile("SmokeSpawner",0,0,5,-2)
		LUGE B 1 Bright A_GunFlash
		LUGE B 0 Bright A_TakeInventory("LugerClipIn",1)
		LUGE B 1 Bright
		LUGE B 1 Bright
		LUGG A 1
		Goto Ready
	Reload:
	    NULL A 0 A_JumpIfInventory("LugerClipIn", 0, "Ready")
		NULL A 0 A_JumpIfInventory("Energy", 1, 1)
		Goto Ready
		LUGR A 1
		LUGR A 3
		LUGR B 3
		LUGR C 3 A_PlayWeaponSound ("Luger/Reload")
		LUGR D 3
		LUGR E 3
		LUGR F 3
		LUGR G 3
		LUGR H 3
		LUGR I 4
		LUGR J 3
		LUGR K 3
		LUGR L 3
		LUGR M 3
		LUGR N 3
		LUGE A 2
	ReloadLoop:
		NULL A 0 A_TakeInventory("Energy", 1)
		NULL A 0 A_GiveInventory("LugerClipIn", 1)
		NULL A 0 A_JumpIfInventory("LugerClipIn", 0, "Ready")
		NULL A 0 A_JumpIfInventory("Energy", 1, "ReloadLoop")
		Goto Ready
	}
}

ACTOR GlockClipAmmo : Inventory { Inventory.MaxAmount 15 }

ACTOR Glock : Weapon
{
	Obituary "%k > GLOCK > %o"
	Weapon.AmmoUse 0
	Weapon.AmmoGive 20
	Weapon.AmmoType "Energy"
	Weapon.AmmoUse2 0
	Weapon.AmmoType2 "Energy"
	+WEAPON.WIMPY_WEAPON
	+INVENTORY.UNDROPPABLE
	States
	{
	Ready:
		GLKF A 1 A_WeaponReady
		GLKF A 0 A_JumpIfInventory("IsReloading", 1, "Reload")
		Loop
	Deselect:
		GLKF A 0 A_Lower
		GLKF A 1 A_Lower
		Loop
	Select:
		GLKF A 0 A_Raise
		GLKF A 1 A_Raise
		Loop
	Fire:
		GLKF A 0 A_JumpIfInventory("GlockClipAmmo", 1, 1)
		Goto Reload
		GLKF A 1
		GLKF A 2 A_FireBullets (1, 1, 1, 11, "BulletPuff")
		GLKF A 0 A_PlaySound ("Glock/Fire")
		GLKF A 0
		GLKF A 0 A_TakeInventory("GlockClipAmmo", 1)
		GLKF bcde 1
		GLKF fghi 1 A_ReFire
		Goto Ready
	AltFire:
		GLKF A 0 A_JumpIfInventory("GlockClipAmmo", 1, 1)
		Goto Reload
		GLKF A 1
		GLKF A 2 A_FireBullets (1, 1, 1, 11, "BulletPuff")
		GLKF A 0 A_PlaySound ("Glock/Fire")
		GLKF A 0
		GLKF A 0 A_TakeInventory("GlockClipAmmo", 1)
		GLKF A 0 A_JumpIfInventory("GlockClipAmmo", 1, 1)
		Goto AltFireEnd
		GLKF b 1
		GLKF A 2 A_FireBullets (1, 1, 1, 11, "BulletPuff")
		GLKF A 0 A_PlaySound ("Glock/Fire")
		GLKF A 0
		GLKF A 0 A_TakeInventory("GlockClipAmmo", 1)
		GLKF A 0 A_JumpIfInventory("GlockClipAmmo", 1, 1)
		Goto AltFireEnd
		GLKF b 1
		GLKF A 2 A_FireBullets (1, 1, 1, 11, "BulletPuff")
		GLKF A 0 A_PlaySound ("Glock/Fire")
		GLKF A 0
		GLKF A 0 A_TakeInventory("GlockClipAmmo", 1)
	AltFireEnd:
		GLKF bcde 1
		GLKF fghiaaaaaaaaaaa 1
		Goto Ready
	Reload:
		GLKR A 0 A_JumpIfInventory("TTClipAmmo", 0, "Ready")
		GLKR A 0 A_JumpIfInventory("Energy", 1, 1)
		Goto Ready
		GLKR SRQPONMLK 1
	ReloadLoop:
		GLKR A 0 A_TakeInventory("Energy", 1)
		GLKR A 0 A_GiveInventory("GlockClipAmmo", 1)
		GLKR A 0 A_JumpIfInventory("GlockClipAmmo", 0, "ReloadEnd")
		GLKR A 0 A_JumpIfInventory("Energy", 1, "ReloadLoop")
	ReloadEnd:
		GLKR JIHGFDCBA 1
		Goto Ready
	}
}

ACTOR TTClipAmmo : Inventory { Inventory.MaxAmount 8 }

ACTOR Cal.45 : Weapon
{
	Obituary "%k > TT > %o"
	weapon.ammogive1 24
	weapon.ammotype1 "Energy"
	weapon.ammouse1 0
	weapon.ammotype2 "Energy"
	weapon.ammouse2 0
	+SPAWNSOUNDSOURCE
	+NOAUTOFIRE
	+INVENTORY.UNDROPPABLE
	States
	{
	Ready:
		CA45 C 1 A_WeaponReady
		NULL A 0 A_JumpIfInventory("IsReloading", 1, "Reload")
		Loop
	Deselect:
		CA45 C 0 A_Lower
		CA45 C 1 A_Lower
		Loop
	Select:
		CA45 C 0 A_Raise
		CA45 C 1 A_Raise
		Loop
	Fire:
		NULL A 0 A_JumpIfInventory("TTClipAmmo", 1, 1)
		Goto Reload
		CA45 C 0
		CA45 B 1 BRIGHT A_PlayWeaponSound("TT/Fire")
		CA45 A 1 BRIGHT A_FireBullets(4.5,5.5,1,20,"BulletPuff")
		CA45 C 1 A_TakeInventory("TTClipAmmo", 1)
		CA45 C 10
		CA45 C 1 A_ReFire
		Goto Ready

	AltFire:
		NULL A 0 A_JumpIfInventory("TTClipAmmo", 1, 1)
		Goto Reload
		CA45 B 1 BRIGHT A_PlayWeaponSound("TT/Fire")
		CA45 A 1 BRIGHT A_FireBullets(4.5,5.5,1,20,"BulletPuff")
		CA45 C 0
		CA45 C 0 A_TakeInventory("TTClipAmmo", 1)

		NULL A 0 A_JumpIfInventory("TTClipAmmo", 1, 1)
		Goto AltFireEnd
		CA45 B 1 BRIGHT A_PlayWeaponSound("TT/Fire")
		CA45 A 1 BRIGHT A_FireBullets(4.5,5.5,1,20,"BulletPuff")
		CA45 C 0
		CA45 C 0 A_TakeInventory("TTClipAmmo", 1)

		NULL A 0 A_JumpIfInventory("TTClipAmmo", 1, 1)
		Goto AltFireEnd
		CA45 B 1 BRIGHT A_PlayWeaponSound("TT/Fire")
		CA45 A 1 BRIGHT A_FireBullets(4.5,5.5,1,20,"BulletPuff")
		CA45 C 0
		CA45 C 0 A_TakeInventory("TTClipAmmo", 1)
	AltFireEnd:
		CA45 C 15
		CA45 C 1 A_ReFire
		Goto Ready

	Reload:
		NULL A 0 A_JumpIfInventory("TTClipAmmo", 0, "Ready")
		NULL A 0 A_JumpIfInventory("Energy", 1, 1)
		Goto Ready
		CL45 BCDEFGH 3
	ReloadLoop:
		NULL A 0 A_TakeInventory("Energy", 1)
		NULL A 0 A_GiveInventory("TTClipAmmo", 1)
		NULL A 0 A_JumpIfInventory("TTClipAmmo", 0, "ReloadEnd")
		NULL A 0 A_JumpIfInventory("Energy", 1, "ReloadLoop")
	ReloadEnd:
		CL45 IJKL 3
		Goto Ready
	}
}

ACTOR ShotgunMagCheck : Ammo
{
	Inventory.Amount 0
	Inventory.MaxAmount 8
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 8
}

ACTOR ShotgunChamberCheck : Inventory
{
	Inventory.MaxAmount 1 
}

ACTOR Boomstick : Weapon
{
	+WEAPON.NOALERT
	+AMMO_OPTIONAL
	+weapon.noautofire
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoType1 "ShotgunMagCheck"
	Weapon.AmmoGive2 8
	Weapon.AmmoGive 0
	Weapon.Kickback 8
	Weapon.AmmoUse 1
	AttackSound "weapons/shotgf"
	Obituary "%k > SHOOTGUN > %o"
	States
	{
	Ready:
		SING A 0 A_JumpIfInventory("IsReloading",1,"Reload")
		SING A 1 A_WeaponReady
		Goto Ready
	Reload:
		SING A 0 A_JumpIfInventory("ShotgunMagCheck",8,2)
		SING A 0 A_JumpIfInventory("Shell",1,2)
		SING A 0 A_WeaponReady
		Goto Ready+1
		SING D 5
		SING GHI 3
		SING J 3 A_PlaySound("singlebarrel/load")
		SING J 0 A_TakeInventory("Shell",1)
		SING J 0 A_GiveInventory("ShotgunMagCheck",1)
		SING K 3
		SING D 5
		SING I 0 A_JumpIfInventory("ShotgunMagCheck",2,2)
		SING I 0 A_GiveInventory("ShotgunChamberCheck",1)
		SING I 0 A_JumpIfInventory("ShotgunChamberCheck",1,3)
		SING I 0 A_JumpIfInventory("ShotgunMagCheck",8,10)
		SING I 0 A_JumpIfInventory("Shell",1,8)
		Goto Ready+2
		SING D 3
		SING E 3  A_PlaySound("singlebarrel/pump")
		SING F 3
		SING ED 3
		SING D 0 A_TakeInventory("ShotgunChamberCheck",999)
		SING D 5 A_WeaponReady
		Goto Ready+2
		SING A 0 A_WeaponReady
		Goto Ready+6
		SING A 0 A_WeaponReady
		Goto Ready
	Deselect: 
		SING A 0 A_PlaySound("weapons/down")
		SING A 0 A_Lower
		SING A 1 A_Lower
		Goto Deselect+2
	Select:
		SING A 0 A_PlaySound("weapons/up")
		SING A 0 A_Raise
		SING A 1 A_Raise
		Goto Select+2
	Fire: 
		SING A 0 A_JumpIfNoAmmo(14)
		SING B 1
		SING C 1 BRIGHT A_FireBullets(5.6,5.6,7,5,"BulletPuff")
		SING C 0 A_FireCustomMissile("CheapAlert",0,0,0,0) 
		SING LA 2
		SING D 3
		SING E 3 A_PlaySound("singlebarrel/pump")
		SING F 3
		SING ED 3
		SING A 2
		SING A 0 //A_Refire
		Goto Ready
		SING A 0 A_WeaponReady
		Goto Ready+2
	Hold: 
		SING A 0 A_JumpIfNoAmmo(14)
		SING B 1
		SING C 1 BRIGHT A_FireBullets(5.6,5.6,7,5,"BulletPuff")
		SING C 0 A_FireCustomMissile("CheapAlert",0,0,0,0) 
		SING LA 2
		SING D 3
		SING E 3 A_PlaySound("singlebarrel/pump")
		SING F 3
		SING ED 3
		SING A 2
		SING A 0 //A_Refire
		Goto Ready    
		SING A 0 A_WeaponReady
		Goto Ready+2
	Flash:
		NULL A 1
		NULL A 1
		stop
	} 
}

Actor ASAmmoInDrum : Inventory { Inventory.MaxAmount 16 }

ACTOR AutoShotgun : Weapon
{
	Obituary "%k > AUTOSHOTGUN > %o"
	Weapon.AmmoType1 "Energy"
	Weapon.AmmoGive1 0
	Weapon.AmmoUse1 0
	Weapon.Kickback 30
	Weapon.SlotNumber 3
	Weapon.UpSound "AUTOSHOGUN/in"
	AttackSound "AUTOSHOTGUN/Fire"
	+NOAUTOFIRE
	+NOALERT
	+INVENTORY.UNDROPPABLE
	States
	{
	Ready:
		ASHG A 1 A_WeaponReady
		NULL A 0 A_JumpIfInventory("IsReloading", 1, "Reload")
		Loop
	Deselect:
		ASHG A 0 A_Lower
		ASHG A 1 A_Lower
		Loop
	Select:
		ASHG A 0 A_Raise
		ASHG A 1 A_Raise
		Loop
	Fire:
		NULL A 0 A_JumpIfInventory("ASAmmoInDrum", 1, 1)
		Goto Reload
		ASHF A 0 A_GunFlash
		ASHF A 0 A_AlertMonsters
		ASHF A 0 A_Recoil(1)
		ASHF A 0 A_FireBullets (6, 4, 5, 5, "BulletPuff")
		ASHF A 0
		ASHF A 2 BRIGHT A_TakeInventory("ASAmmoInDrum", 1)
		ASHF B 2 BRIGHT
		ASHG BCDCBA 1
		ASHG A 2
		Goto Ready
	Flash:
		TNT1 A 1 A_Light2
		TNT1 A 1 A_Light1
		TNT1 A 0 A_Light0
		Stop
	Reload:
		NULL A 0 A_JumpIfInventory("ASAmmoInDrum", 0, "Ready")
		NULL A 0 A_JumpIfInventory("Energy", 1, 1)
		ASHR ABCDEFGHIJ 3
	ReloadLoop:
		NULL A 0 A_TakeInventory("Energy", 1)
		NULL A 0 A_GiveInventory("ASAmmoInDrum", 1)
		NULL A 0 A_JumpIfInventory("ASAmmoInDrum", 0, "ReloadEnd")
		NULL A 0 A_JumpIfInventory("Energy", 1, "ReloadLoop")
	ReloadEnd:
		ASHR KLMNOPRS 3
		Goto Ready
	}
}

ACTOR SSG : Weapon
{
	Obituary "%k > SSG > %o"
	Decal "BulletChip"
	Weapon.AmmoType "Energy"
	Weapon.AmmoGive 25
	Weapon.AmmoUse 2
	+INVENTORY.UNDROPPABLE
	States
	{
	Ready:
		REMG A 1 A_WeaponReady
		Loop
	Deselect:
		REMG A 0 A_Lower
		REMG A 1 A_Lower
		Loop
	Select:
		REMG A 0 A_Raise
		REMG A 1 A_Raise
		Loop
	Fire:
		REMG A 0 A_PlaySound ("SSG/Fire")
		REMG A 0
		REMG B 2 Bright A_FireBullets(3,6,10,5,"BulletPuff",1)
		REMG C 2
		REMG D 2
		REMG E 2
		REMG F 2 A_PlaySound ("SSG/Open")
		REMG F 6
		REMG GHI 1
		REMG JJ 1 A_PlaySound ("SSG/In")
		REMG K 3
		REMG L 2 A_PlaySound ("SSG/Close")
		REMG A 3
		Goto Ready
	}
}

ACTOR UZIClipAmmo : Inventory { Inventory.MaxAmount 30 }

ACTOR "SMG-UZI" : Weapon
{
	Obituary "%k > UZI > %o"
	Weapon.AmmoUse 0
	Weapon.AmmoGive 30
	Weapon.AmmoType "Energy"
	attacksound "UZI/fire"
	+INVENTORY.UNDROPPABLE
	States
	{
	Ready:
		UZI1 A 1 A_WeaponReady
		UZI1 A 0 A_JumpIfInventory("IsReloading", 1, "Reload")
		Loop
	Deselect:
		UZI1 A 0 A_Lower
		UZI1 A 1 A_Lower
		Loop
	Select:
		UZI1 A 0 A_Raise
		UZI1 A 1 A_Raise
		Loop
	Fire:
		UZI1 A 0 A_JumpIfInventory("UZIClipAmmo", 1, 1)
		Goto Reload
		UZI1 B 1 bright A_FireBullets (3, 2, 1, 3)
		UZI1 A 0
		UZI1 A 0 A_TakeInventory("UZIClipAmmo", 1)
		UZI1 CC 1
		UZI1 A 1 A_Refire
		Goto Ready
	Reload:
		UZIR A 0 A_JumpIfInventory("UZIClipAmmo", 0, "Ready")
		UZIR A 0 A_JumpIfInventory("Energy", 1, 1)
		UZIR SRQP 2
		UZIR EFGHI 2
	ReloadLoop:
		UZIR A 0 A_TakeInventory("Energy", 1)
		UZIR A 0 A_GiveInventory("UZIClipAmmo", 1)
		UZIR A 0 A_JumpIfInventory("UZIClipAmmo", 0, "ReloadEnd")
		UZIR A 0 A_JumpIfInventory("Energy", 1, "ReloadLoop")
	ReloadEnd:
		UZIR JKLMNOPQRS 2
		Goto Ready
	}
}

ACTOR M16LoadedBullet : Inventory { Inventory.MaxAmount 30 }

ACTOR M16 : Weapon
{
	Obituary "%k > M16 > %o"
	Weapon.AmmoUse1 0
	Weapon.AmmoUse2 0
	Weapon.AmmoGive1 30
	Weapon.AmmoGive2 0
	Weapon.AmmoType1 "Energy"
	States
	{
	Ready:
		ASSU A 1 A_WeaponReady
		ASSU A 0 A_JumpIfInventory("IsReloading",1,"Reload")
		Goto Ready
	Deselect:
		ASSU A 0 A_Lower
		ASSU A 1 A_Lower
		Loop
	Select:
		ASSU A 0 A_Raise
		ASSU A 1 A_Raise
		Loop
	Fire:
		ASSU A 0 A_JumpIfInventory("M16LoadedBullet", 1, 1)
		Goto NoAmmo
		ASSU A 2 A_PlayWeaponSound("m16/fire")
		ASSU B 2 A_FireBullets (2,3,1,7,"BulletPuff",1)
		ASSU B 0 A_TakeInventory("M16LoadedBullet", 1)
		ASSU C 0 A_JumpIfInventory("M16LoadedBullet", 1, "ReFire")
		Goto Reload
	ReFire:
		ASSU C 0 A_ReFire
		Goto Ready
	NoAmmo:
		ASSU A 0 A_JumpIfInventory("Energy", 1, "Reload")
		ASSU A 1 A_PlaySound("m16/noammo")
		Goto Ready
	Reload:
		ASSU A 0 A_JumpIfInventory("M16LoadedBullet",0,"Ready")
		ASSU A 0 A_JumpIfInventory("Energy", 1, 1)
		Goto NoReload
		ASSU D 25 A_PlaySound("m16/reload")
		ASSU E 14
		ASSU E 0 A_SpawnItem("UsedM16Clip")
	ReloadLoop:
		ASSU A 0 A_TakeInventory("Energy", 1)
		ASSU A 0 A_GiveInventory("M16LoadedBullet", 1)
		ASSU A 0 A_JumpIfInventory("M16LoadedBullet", 0, "Ready")
		ASSU A 0 A_JumpIfInventory("Energy", 1, "ReloadLoop")
		Goto Ready
	NoReload:
		ASSU D 1
		Goto Ready
	}
}

ACTOR UsedM16Clip
{
	states
	{
	Spawn:
		CLIP A -1
		stop
	}
}

ACTOR AK47Ammo : Inventory { Inventory.MaxAmount 30 }

ACTOR AK47 : Weapon
{
	Obituary "%k > AK47 > %o"
	Weapon.AmmoType "Energy"
	Weapon.AmmoGive 30
	Weapon.AmmoUse 1
	AttackSound "AK47/FIRE"
	SCALE	0.08
	+INVENTORY.UNDROPPABLE
	States
	{
	Ready:
		AK47 A 1 A_WeaponReady
		//AK47 A 0 A_JumpIfInventory("IsReloading", 1, "Reload")
		Loop
	Deselect:
		AK47 A 0 A_Lower
		AK47 A 1 A_Lower
		Loop
	Select:
		AK47 A 0 A_Raise
		AK47 A 1 A_Raise
		Loop
	Fire:
		AK47 BC 1 bright A_FireBullets (3, 2, 1, 7)
		AK47 C 0 bright Radius_Quake(1, 2, 0, 1, 0)
		AK47 B 0 bright A_SetPitch (pitch-0.1)
		AK47 C 1 A_SetPitch (pitch-0.075)
		AK47 D 1
		AK47 A 1 A_Refire
		goto Ready
	/*Fire:
		AK47 C 0 A_JumpIfInventory("AK47Ammo", 1, 1)
		Goto Reload
		AK47 C 2
		AK47 U 2 BRIGHT A_FireBullets(1,0,-1,5+random(-1,5),"BulletPuff",1)
		AK47 A 0 A_TakeInventory("AK47Ammo", 1)
		AK47 A 0 A_ReFire
		Goto Ready
	ReFire:
		AK47 C 0 A_JumpIfInventory("AK47Ammo", 1, 1)
		Goto Reload
		AK47 C 2
		AK47 U 2 BRIGHT A_FireBullets(5.6,0,-1,7+random(-1,7),"BulletPuff",1)
		AK47 A 0 A_TakeInventory("AK47Ammo", 1)
		AK47 A 0 A_Refire
		Goto Ready
	Reload:
		AK47 A 0 A_JumpIfInventory("AK47Ammo", 0, "Ready")
		AK47 A 0 A_JumpIfInventory("Energy", 1, 1)
		Goto Ready
		AK47 A 1
		AK47 D 0 A_PlaysoundEx("AK47/CLIPOUT","Weapon")
		AK47 DEFG 4
		AK47 H 0 A_PlaysoundEx("AK47/CLIPIN","Weapon")
		AK47 HIJK 6
		AK47 L 0 A_PlaysoundEx("AK47/COCK","Weapon")
		AK47 LMNOPQRST 3
	ReloadLoop:
		AK47 A 0 A_TakeInventory("Energy", 1)
		AK47 A 0 A_GiveInventory("AK47Ammo", 1)
		AK47 A 0 A_JumpIfInventory("AK47Ammo", 0, "Ready")
		AK47 A 0 A_JumpIfInventory("Energy", 1, "ReloadLoop")*/
	}
}

ACTOR Minigun : Weapon
{
	Obituary "%k > MINIGUN > %o"
	Weapon.AmmoUse1 1
	Weapon.AmmoGive1 30
	Weapon.AmmoType1 "Energy"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 30
	Weapon.AmmoType2 "Energy"
	AttackSound "DSTOMFR"
	+INVENTORY.UNDROPPABLE
	States
	{
	Select:
		TOMG A 0 A_Raise
		TOMG A 1 A_Raise
		loop
	Deselect:
		TOMG A 0 A_Lower
		TOMG A 1 A_Lower
		loop
	Ready:
		TOMG A 1 A_weaponready
		loop
	Fire:
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOMF A 2 bright A_firebullets(1,1,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOMF B 2 bright A_firebullets(1,1,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOMF C 2 bright A_firebullets(1,1,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOMF D 2 bright A_firebullets(1,1,1,6,"BulletPuff")
		goto ready
	AltFire:
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOF2 E 2 bright A_firebullets(20,6,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOF2 F 2 bright A_firebullets(20,6,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOF2 G 2 bright A_firebullets(20,6,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOF2 H 2 bright A_firebullets(20,6,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOF2 I 2 bright A_firebullets(20,6,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOF2 J 2 bright A_firebullets(20,6,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOF2 K 2 bright A_firebullets(20,6,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOF2 L 2 bright A_firebullets(20,6,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOF2 K 2 bright A_firebullets(20,6,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOF2 J 2 bright A_firebullets(20,6,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOF2 I 2 bright A_firebullets(20,6,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOF2 H 2 bright A_firebullets(20,6,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOF2 G 2 bright A_firebullets(20,6,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOF2 F 2 bright A_firebullets(20,6,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOF2 E 2 bright A_firebullets(20,6,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOF2 D 2 bright A_firebullets(20,6,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOF2 C 2 bright A_firebullets(20,6,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOF2 B 2 bright A_firebullets(20,6,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOF2 A 2 bright A_firebullets(20,6,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOF2 B 2 bright A_firebullets(20,6,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOF2 C 2 bright A_firebullets(20,6,1,6,"BulletPuff")
		TOMF A 0 A_GunFlash
		TOMF A 0 A_JumpifNoAmmo("Ready")
		TOF2 D 2 bright A_firebullets(20,6,1,6,"BulletPuff")
		TOMF A 0 A_ReFire
		goto ready
	}
}

ACTOR HandGrenade : Weapon
{
	Inventory.PickupMessage "You got a hand grenade!"
	Weapon.AmmoType "HandGrenadeAmmo"
	Weapon.AmmoGive 1
	Weapon.AmmoUse 1
	+INVENTORY.UNDROPPABLE
	+WEAPON.NOAUTOFIRE
	+NOALERT
	States
	{
	Ready:
		GREN A 6 A_WeaponReady
		Loop
	Deselect:
		GREN A 0 A_Lower
		GREN A 1 A_Lower
		Loop
	Select:
		GREN A 0 A_Raise
		GREN A 1 A_Raise
		Loop
	Fire:
		GRNF A 4
		GRNF B 4 A_PlaySound("Grenade/PullPin") 
		GRNF C 4
		GRNF D 3 A_PlaySound("Grenade/Toss")
		GRNF E 3 
		GRNF F 3 A_FireCustomMissile("ThrownGrenade",0,1,0,0)
		Goto Ready
	}
}

ACTOR HandGrenadeAmmo : Ammo
{
	Inventory.Amount 1
	Inventory.MaxAmount 25
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 50
	Inventory.PickupMessage "You should not see this message, the weapon is the ammo"
	Scale 0.5
	States
	{
	Spawn:
		NULL D -1
		Loop
	}
}

ACTOR ThrownGrenade
{
	Radius 5
	Height 5
	Speed 24
	Damage 10
	SeeSound "weapons/grnbounce"
	PROJECTILE
	-NOGRAVITY
	+HEXENBOUNCE
	+LOWGRAVITY
	States
	{
	Spawn:
		GRND A 0
		GRND A 4 A_AlertMonsters
		GRND BCDEFGH 4
		Goto Death
	Death:
		MISL B 0 A_PlaySound("Grenade/Explode")
		MISL B 0 A_Explode (150, 350, 0)
		MISL BCD 3 Bright
		Stop
	}
}

ACTOR Mine : Weapon
{
	Obituary "%k > MINE > %o"
	Weapon.AmmoType1 "HandGrenadeAmmo"
	Weapon.AmmoType2 "HandGrenadeAmmo"
	Weapon.AmmoGive 2
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	+WEAPON.NOALERT
	+INVENTORY.UNDROPPABLE
	States
	{
	Ready:
		MINB A 1 A_WeaponReady
		Loop
	Deselect:
		MINB A 0 A_Lower
		MINB A 1 A_Lower
		Loop
	Select:
		MINB A 0 A_Raise
		MINB A 1 A_Raise
		Loop
	Fire:
		MIND I 5
		MIND ABCDEFGH 1
		TNT1 A 7 A_FireCustomMissile("MineThing",0,1,0,0)
		MINC HGFEDCB 1
		MINC A 3 A_ReFire
		Goto Ready
	AltFire:
		MIND I 5
		MIND ABCDEFGH 1
		TNT1 A 5
		CAB2 ABCDE 1
		TNT1 A 0 A_PlayWeaponSound("MINE002")
		CAB2 FG 1
		CAB2 H 2 A_FireCustomMissile("MineThing2",0,1,8,8)
		CAB2 GFEDCBA 1
		TNT1 A 3
		MINC HGFEDCB 1
		MINC A 3 A_ReFire
		Goto Ready
	}
}

ACTOR MineDeathMarker : Inventory
{
	Inventory.Amount 1
	Inventory.MaxAmount 1
}

ACTOR MineThing
{
	Radius 8
	Height 5
	Damage 5
	SeeSound "MINE001"
	Speed 0
	PROJECTILE
	+DONTSPLASH
	+MISSILE
	speed 0
	Health 20
	Mass 0x7fffffff
	-ACTIVATEIMPACT
	+DOOMBOUNCE
	+THRUGHOST
	+MTHRUSPECIES
	+FLOORCLIP
	+FRIENDLY
	-NOGRAVITY
	+PUSHABLE
	+SHOOTABLE
	+SOLID
	States
	{
	Spawn:
		MINE A 0
		MINE A 0 ACS_ExecuteWithResult(777,10)
		MINE A 0 A_Gravity
		MINE AAABBB 4
		Goto Spawn+2
	Death:
		MINE A 1 A_LookEx(0,0,16,16,360)
		loop
	See:
		MISL B 0 A_PlaySound("weapons/rocklx")
		MISL B 8 A_Explode
		MISL C 6
		MISL D 4
		MISL D 0 ACS_ExecuteWithResult(777,11)
		Stop
	}
}

ACTOR MineThing2 : MineThing
{
	Speed 15
}

ACTOR Flame : FireSpawner
{
	Speed 25
	Radius 16
	Height 16
	Damage (random(1,4))
	reactiontime 16
	+RIPPER
	+BLOODLESSIMPACT
	PROJECTILE
}

ACTOR FLAMAmmo : Inventory
{
	Inventory.Amount 1
	Inventory.MaxAmount 100
}

ACTOR FlameThrower : Weapon
{
	Weapon.AmmoType "Energy"
	Weapon.AmmoGive 100
	Weapon.AmmoUse 0
	Weapon.Kickback 400
	AttackSound "FlameThrower/Fire"
	+INVENTORY.UNDROPPABLE
	States
	{
	Ready:
		FLAM A 1 A_WeaponReady
		NULL A 0 A_JumpIfInventory("IsReloading", 1, "Reload")
		Loop
	Deselect:
		FLAM A 0 A_Lower
		FLAM A 1 A_Lower
		Loop
	Select:
		FLAM A 0 A_Raise
		FLAM A 1 A_Raise
		Loop
	Fire:
		FLAM A 0 A_JumpIfInventory("FLAMAmmo", 1, 1)
		Goto Reload
		FLAM A 0 A_PlayWeaponSound("weapons/flamer")
		FLAF A 2 BRIGHT A_FireCustomMissile("Flame", 0, 1, 3, 1, 1)
		FLAF A 0 A_TakeInventory("FLAMAmmo", 1)
		FLAF A 0 A_ReFire
		FLAF BCDEF 3 BRIGHT
		Goto Ready
	Reload:
		NULL A 0 A_JumpIfInventory("FLAMAmmo", 0, "Ready")
		NULL A 0 A_JumpIfInventory("Energy", 1, 2)
		FLAM A 0 A_PlaySound("misc/noammo")
		Goto Ready
		FLAM A 1 A_FireCustomMissile("FLAMCasGren",0,0,0,0,0)
		FLAM A 4
		FLAM BC 4
		FLAR P 4
		FLAR ABCD 1
		FLAR E 1 A_PlaySound("weapons/flamout")
		FLAR FGHIJKLMNO 1
	ReloadLoop:
		NULL A 0 A_TakeInventory("Energy", 1)
		NULL A 0 A_GiveInventory("FLAMAmmo", 1)
		NULL A 0 A_JumpIfInventory("FLAMAmmo", 0, "ReloadEnd")
		NULL A 0 A_JumpIfInventory("Energy", 1, "ReloadLoop")
		Goto ReloadEnd
	ReloadEnd:
		FLAR P 15 A_PlaySound("weapons/flamin")
		FLAR ONMLKJIHGFE 1
		FLAR CBA 1
		FLAR P 1
		FLAM CBA 1
		Goto Ready
	}
}

actor FLAMCasGren
{
	Height 12
	Radius 9
	Speed 15
	Scale 0.9
	PROJECTILE
	-NOGRAVITY
	+DOOMBOUNCE
	+THRUGHOST
	DeathSound "world/barrelx"
	Obituary "%o went boom."
	states
	{
	Spawn:
		FAMM A 1
		Loop
	Death:
		MISL B 8 Bright A_Explode
		MISL C 6 Bright
		MISL D 4 Bright
		Stop
	}
}

ACTOR M40 : Weapon // SniperRifle
{
	Obituary "%k > M40 > %o"
	radius 20
	height 16
	weapon.kickback 125
	weapon.ammotype1 "Energy"
	weapon.ammouse1 1
	weapon.ammogive1 12
	weapon.ammotype2 ""
	weapon.ammouse2 0
	weapon.ammogive2 0
	+NOALERT
	Scale 0.80
	+WEAPON.NOAUTOFIRE
	+INVENTORY.UNDROPPABLE
	states
	{
	Ready:
		SNPG A 1 A_WeaponReady
		Loop
	Ready2:
		SNPS A 1 A_WeaponReady(1)
		Loop
	Deselect:
		SNPG A 0 A_Lower
		SNPG A 1 A_Lower
		Loop
	Select:
		SNPG A 0 A_Raise
		SNPG A 1 A_Raise
		Loop
	Fire:
		SNPG A 0 A_JumpIfInventory("IronSightSniper",1,"Fire2")
		SNPG A 0 A_AlertMonsters
		SNPG A 0 A_FireBullets (0.0, 0.0, -1, 115, "NoM40RifleGibPuff")
		SNPG A 0 A_PlayWeaponSound("M40Rifle/Fire")
		SNPF AB 2
		SNPF CDE 3
		SNPG A 3
		SNPG BC 3
		SNPG DEF 2
		SNPG H 2 A_PlayWeaponSound("M40Rifle/Bolt1")
		SNPG IJ 2
		SNPG K 3
		SNPG JI 2
		SNPG H 2 A_PlayWeaponSound("M40Rifle/Bolt2")
		SNPG FED 3
		SNPG CB 4
		SNPG A 2
		SNPG A 1 A_ReFire
		Goto Ready
	Fire2:
		SNPS A 0 A_AlertMonsters
		SNPS A 0 A_FireBullets (0.0, 0.0, -1, 115, "NoM40RifleGibPuff")
		SNPS A 0 A_PlayWeaponSound("M40Rifle/Fire")
		SNPS A 2
		SNPS A 3
		SNPS A 3
		SNPS A 3
		SNPS A 2
		SNPS A 2 A_PlayWeaponSound("M40Rifle/Bolt1")
		SNPS A 2
		SNPS A 3
		SNPS A 2
		SNPS A 2 A_PlayWeaponSound("M40Rifle/Bolt2")
		SNPS A 3
		SNPS A 4
		SNPS A 2
		SNPS A 1 A_ReFire
		Goto Ready2
	AltFire:
		SNPS A 0
		SNPS A 0 A_JumpIfInventory("IronSightSniper",1,"AltFire2")
		SNPS A 0 A_Giveinventory("IronSightSniper",1)
		SNPS A 0 A_PlaySound ("SRZOOM", CHAN_WEAPON)
		SNPS A 0 A_ZoomFactor(4.0, ZOOM_INSTANT)
		SNPS A 1
		Goto Ready2
	AltFire2:
		SNPS A 1
		SNPS A 0 A_Takeinventory("IronSightSniper",1)
		SNPS A 0 A_PlaySound ("SRZOOM", CHAN_WEAPON)
		SNPS A 0 A_ZoomFactor(1.0, ZOOM_INSTANT)
		Goto Ready
	}
}

ACTOR IronSightSniper : Inventory { Inventory.MaxAmount 1 }

ACTOR NoM40RifleGibPuff : BulletPuff
{
	+NOEXTREMEDEATH
}
